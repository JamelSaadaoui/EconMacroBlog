*
* Farrant and Peersman(2006), "Is the Exchange Rate a Shock Absorber or a Source
* of Shocks? New Empirical Evidence", JMCB, vol 38, no. 4, pp 939-961.
*
* Sign restrictions with 3 variable model
*
open data "datasetfp.xlsx"
calendar(q) 1961:1
data(format=xlsx,org=columns) 1961:01 2003:02 yus yemu yuk yjp yca cpus cpemu cpuk cpjp cpca stus stemu $
 stuk stjp stca bxemu bxuk bxjp bxca
*
compute nsteps=29
compute VARDraws=500
*
* y = (real) output, cp = consumer prices, st = interest rate differential,
* bx is bilateral nominal exchange rate (local currency/US$)
*
dec hash[string] longname
compute longname("uk")="United Kingdom"
compute longname("emu")="Euro Area"
compute longname("jp")="Japan"
compute longname("ca")="Canada"
*
source mcsignrestrictions.src
*
compute nshocks=3,nvar=3
dec vect[strings] sl(nshocks) vl(nvar)
*compute sl=||"Supply","Demand","Nominal"||
compute sl=||"Supply","Demand"||
compute vl=||"Y-Y*","P-P*","Q"||
*
* Where signs are restricted, it's from steps 1 to 4 (counting 1 as the impact)
* for the output growth differential and prices and just the impact for the real
* exchange rate
*
dec SignDesc SupplyDesc(2)
compute SupplyDesc(1)=||+1,1,4||
compute SupplyDesc(2)=||-2,1,4||
dec SignDesc DemandDesc(3)
compute DemandDesc(1)=||+1,1,4||
compute DemandDesc(2)=||+2,1,4||
compute DemandDesc(3)=||-3,1,1||
dec SignDesc NominalDesc(3)
compute NominalDesc(1)=||+1,1,4||
compute NominalDesc(2)=||+2,1,4||
compute NominalDesc(3)=||+3,1,1||
*
*compute [vect[SignDesc]] SignConstraints=||SupplyDesc,DemandDesc,NominalDesc||
compute [vect[SignDesc]] SignConstraints=||SupplyDesc,DemandDesc||
*
********************************************************************************
*
*
dofor [string] zone = "uk" "emu" "jp" "ca"
   *
   * For each zone, compute
   *   100*log ratio of prices vs US
   *   100*log real exchange rate
   *   100*log ratio of output vs US
   *
   set lp  = 100.0*(log(%s("cp"+zone))-log(cpus))
   set lrx = 100.0*log(%s("bx"+zone))+lp
   set ly  = 100.0*(log(%s("y"+zone))-log(yus))
   set dp  = lp-lp{1}
   set drx = lrx-lrx{1}
   set dy  = ly-ly{1}
   @varlagselect(lags=8,crit=aic,model=var3) 1974:1 2002:4
   # dy drx dp
   estimate(model=var3) 1974:1 2002:4
   @MCSignRestrictions(model=var3,nsteps=nsteps,constraints=SignConstraints,$
       accum=||1,2,3||,ndraws=VARDraws,expand)
   @MCGraphIRF(model=var3,shocklabels=sl,varlabels=vl,$
       header="Sign Restrictions, 3 Variable Model, Zone "+longname(zone),picture="##.##",center=median)
   @MCFEVDTable(model=var3,shocklabels=sl,varlabels=vl,horizons=||1,4,20||,stddev=1.0)
end dofor
